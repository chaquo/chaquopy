apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.chaquo.python'  // Must come after com.android.application

// *** Remove in public repository ********************************************
import com.chaquo.python.Common;
// **********************************************************************

afterEvaluate {
    def assetsSrcDir = "src/main/assets/source"
    delete assetsSrcDir
    mkdir assetsSrcDir
    for (filename in ["src/main/python/chaquopy/demo/ui_demo.py",
                      "src/main/java/com/chaquo/python/demo/JavaDemoActivity.java"]) {
        assert file(filename).exists()
        copy {
            from filename
            into assetsSrcDir
        }
    }
}

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "com.chaquo.python.demo3"
        minSdkVersion 16
        targetSdkVersion 30

        // *** Replace in public repository **********************************
        // Final digit of versionCode is 0 for -SNAPSHOT builds in this repo, and 1 for released
        // builds in the public repo.
        versionName rootProject.version + "-SNAPSHOT"
        def verParsed = rootProject.version.split(/\./).collect { Integer.parseInt(it) }
        versionCode ((verParsed[0] * 1000000) + (verParsed[1] * 1000) + (verParsed[2] * 10) + 0)

        ndk {
            abiFilters Common.ABIS.toArray()
        }
        // **********************************************************************

        python {
            // If Chaquopy fails to find Python on your build machine, enable the following
            // line and edit it to point to Python 3.5 or later.
            // buildPython "C:/Python36/python.exe"

            // Android UI demo
            pip {
                install "Pygments==2.2.0"   // Also used in Java API demo
            }
            staticProxy "chaquopy.demo.ui_demo"

            // Python unit tests
            pip {
                install "murmurhash==0.28.0"  // Requires chaquopy-libcxx
            }
            staticProxy("chaquopy.test.static_proxy.basic", "chaquopy.test.static_proxy.header",
                        "chaquopy.test.static_proxy.method")
            pyc {
                // For testing bytecode compilation on device, and also to include test source
                // code in stack traces.
                src false

                // *** Remove in public repository ***************************************
                pip true  // For testing bytecode compilation during build.
                // ***********************************************************************
            }
        }

        // Chaquopy generates extra internal-use constructors on static proxy classes.
        lintOptions {
            disable "ValidFragment"
        }

        // Remove other languages imported from Android support libraries.
        resConfigs "en"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // *** Remove in public repository ***************************************
    def keystore = file('../chaquo.jks')
    if (keystore.exists()) {
        signingConfigs {
            config {
                storeFile keystore
                keyAlias 'key0'
                keyPassword 'android'
                storePassword 'android'
            }
        }
        buildTypes.all { it.signingConfig signingConfigs.config }
    }

    sourceSets {
        for (path in [
            "../../product/runtime/src/test",   // Unit tests
            "src/utils"                         // Files shared with pkgtest app
        ]) {
            main {
                java { srcDir "$path/java" }
                python { srcDir "$path/python" }
                res { srcDir "$path/res" }
            }
        }
    }
    // **********************************************************************
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // appcompat version 1.2.0 is required to fix an incompatibility with WebView on API level
    // 21 (https://stackoverflow.com/questions/41025200).
    implementation 'androidx.appcompat:appcompat:1.2.0-beta01'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'junit:junit:4.12'
    testImplementation 'junit:junit:4.12'
}
